# parent package with with targets in components
# components are expected to contain the actual target
# each component's config should be in a equally named subdirectory, i.e.: ${CMAKE_INSTALL_DATAROOTDIR}/cmake/@PROJECT_NAME@/${component}/${component}-config.cmake


file(GLOB hypertrie_available_components LIST_DIRECTORIES true ${CMAKE_CURRENT_LIST_DIR}/*)

# available components are listed here
set(@PROJECT_NAME@_available_components hypertrie einsum)

# check if the user provided components are actually available
foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})
    if(NOT component IN_LIST @PROJECT_NAME@_available_components)
        message(FATAL_ERROR "Component ${component} does not exist in package @PROJECT_NAME@. Available components are ${@PROJECT_NAME@_available_components}")
    endif()
endforeach()

# default component @PROJECT_NAME@::@PROJECT_NAME@ is always included
include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@/@PROJECT_NAME@-config.cmake)

# include all listed components
foreach(component ${@PROJECT_NAME@_FIND_COMPONENTS})
    include(${CMAKE_CURRENT_LIST_DIR}/${component}/${component}-config.cmake)
endforeach()
